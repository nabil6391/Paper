apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}

ext {
    PUBLISH_GROUP_ID = 'io.github.pilgr'
    PUBLISH_VERSION = '2.7.2'
    PUBLISH_ARTIFACT_ID = 'paperdb'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    compileSdkVersion 29
    // Must be the same version as in travis.xml

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        consumerProguardFiles 'proguard.txt'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }
    sourceSets {
        test.java.srcDirs += 'src/androidTest/kotlin'
    }
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("androidTest"))
        {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

dependencies {
    api 'com.esotericsoftware:kryo:4.0.1'
    api 'de.javakaffee:kryo-serializers:0.40'
    implementation "androidx.appcompat:appcompat:1.3.1"

    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'com.orhanobut:hawk:1.14'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.1.0'
    androidTestImplementation 'joda-time:joda-time:2.9.6'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}